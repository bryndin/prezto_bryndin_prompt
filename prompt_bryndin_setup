#
# My custom 2 line prompt
#
# Features:
#   - full terminal width top line to visually separate different runs
#   - current time
#   - current user
#   - host (only shown on remote machines)
#   - active pythons virtualenv
#   - git info
#   - current dir (automatically falls back to shortened version if too long)
#
# Authors:
#   Dmitry Bryndin <bryndin@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/b2M5eOw.png?1
#

prompt_color="cyan"
prompt_time_color="27"
prompt_host_color="27"
prompt_virtualenv_color="yellow"
prompt_dir_color="33"

# Load dependencies.
pmodload 'helper' 'python' 'git'

function prepare_header {
  # do all substitutions and expansions
  local left=${(e)_left_header}
  local right=${(e)_right_header}

  # Pattern listing visual modifiers
  local zero='%([BSUbfksu]|([FB]|){*})'

  # remove non-printable prompt_color characters to compute length correctly
  local left_width=${#${(S%%)left//$~zero/}}
  local right_width=${#${(S%%)right//$~zero/}}

  _filler_width=$(( $COLUMNS - 1 - $left_width - $right_width ))
}

function prompt_bryndin_precmd {
  function prepare_full_pwd {
    [[ -w $PWD ]] && print -n '%d' || print -n '%F{red}%d%f'
  }

  function prepare_compressed_pwd {
    local pwd="${PWD/#$HOME/~}"

    if [[ "$pwd" == (#m)[/~] ]]; then
      print -n "$MATCH"
      unset MATCH
    else
      print -n "${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
    fi
  }

  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  _cur_dir="$(prepare_full_pwd)"

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Virtualenv information. 
  python-info

  # Prepare header line
  prepare_header

  local filler=""
  # fallback to compressed PWD if blocks aren't fitting
  [[ _filler_width -lt 0 ]] && _cur_dir="$(prepare_compressed_pwd)" && prepare_header
  [[ _filler_width -gt 0 ]] && for x ({0..${_filler_width}}) filler+='─'

  # print with expansion
  print -P ${_left_header}%F{$prompt_color}${filler}%f${_right_header}
}

function prompt_bryndin_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_bryndin_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%F{green}❮%F{yellow}❮%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format ':%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' "%F{$prompt_color}─(%F{blue}git%f$(coalesce "%b" "%p" "%c")%s%A%B%S%a%d%m%r%U%u%F{$prompt_color})%f"

  # Set python-info (virtualenv) parameters. 
  zstyle ':prezto:module:python:info:virtualenv' format "%F{$prompt_color}─<%F{$prompt_virtualenv_color}%v%F{$prompt_color}>%f"

  ### Header blocks
  local time='%T'
  local time_block="%F{$prompt_color}─(%F{$prompt_time_color}${time}%F{$prompt_color})%f"

  local host=''
  [[ -n $SSH_CLIENT ]] && host='%F{$prompt_host_color}@%m%f'
  local host_block="%F{$prompt_color}─(%f%(!.%F{red}%n%f.%F{$prompt_host_color}%n%f$host)%F{$prompt_color})%f"

  local git_block='${git_info:+${(e)git_info[prompt]}}'

  local virtualenv_block='${python_info[virtualenv]}'

  local dir_block='%F{$prompt_color}[%F{$prompt_dir_color}${_cur_dir}%F{$prompt_color}]─%f'

  # Define prompts
  _left_header="${time_block}${host_block}${virtualenv_block}${git_block}"
  _right_header="${dir_block}"
  PROMPT='%(!. %    B%F{red}#%f%b.)${editor_info[keymap]}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '

  # Disable VirtualEnv prompt
  VIRTUAL_ENV_DISABLE_PROMPT=1
}

prompt_bryndin_setup "$@"
